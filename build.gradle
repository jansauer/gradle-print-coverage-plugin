plugins {
  id 'com.palantir.git-version' version '0.11.0'
  id 'groovy'
  id 'java-gradle-plugin'
  id 'jacoco'
  id 'com.gradle.plugin-publish' version '0.10.1'
}

description = 'Gradle print coverage plugin'
group = 'de.jansauer.printcoverage'
version = gitVersion()

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.codehaus.groovy:groovy-all:2.5.4'
  compile 'org.apache.ant:ant:1.9.4'

  testCompile('org.spockframework:spock-core:1.2-groovy-2.5') {
    exclude(module: 'groovy-all')
  }
}

pluginBundle {
  website = 'https://github.com/jansauer/gradle-print-coverage-plugin'
  vcsUrl = 'https://github.com/jansauer/gradle-print-coverage-plugin'
  description = 'Gradle print coverage plugin.'
  tags = ['coverage', 'jacoco', 'gitlab']

  plugins {
    greetingsPlugin {
      id = 'de.jansauer.printcoverage'
      displayName = 'Gradle print coverage plugin'
    }
  }
}

task codeCoverageReport(type: JacocoReport) {
  executionData fileTree(project.rootDir.absolutePath).include('**/build/jacoco/*.exec')
  sourceSets sourceSets.main

  reports {
    xml.enabled true
    xml.destination file("${buildDir}/reports/jacoco/report.xml")
    html.enabled false
  }
}
